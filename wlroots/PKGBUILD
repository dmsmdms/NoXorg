# Maintainer: Maksym Siverskyi <dmsmaxsv@gmail.com>

pkgbase="wlroots"
pkgname=("libwlroots10" "libwlroots-dev")
pkgdesc="Modular wayland compositor library"
pkgurl="https://gitlab.archlinux.org/archlinux/packaging/packages/${pkgbase}/-/raw/main/PKGBUILD?inline=false"
pkgver="$(wget -O - ${pkgurl} 2> /dev/null | grep pkgver= | cut -d '=' -f 2-)"
pkgrel=1
arch=("$(dpkg --print-architecture)")
makedepends=("gcc" "meson" "pkg-config" "hwdata" "libc6-dev" "libdrm-dev" "libinput-dev" "libudev-dev" "libgbm-dev"
			 "libxkbcommon-dev" "libpixman-1-dev" "libvulkan-dev" "libwayland-dev" "libseat-dev")
source=("https://gitlab.freedesktop.org/${pkgbase}/${pkgbase}/-/releases/${pkgver}/downloads/${pkgbase}-${pkgver}.tar.gz")
sha256sums=("SKIP")
prefix="/usr/local"

build() {
	cd ${pkgbase}-${pkgver}

	meson setup build \
	--prefix="${prefix}" \
	-Dallocators=gbm \
	-Dbackends=drm,libinput \
	-Dexamples=false \
	-Drenderers=vulkan \
	-Dxcb-errors=disabled \
	-Dxwayland=disabled \
	-Ddebug=false \
	-Dwerror=false \
	-Doptimization=3 \
	-Dbackend_max_links=$(nproc) \
	-Db_asneeded=true \
	-Db_lto=true \
	-Db_lto_threads=$(nproc) \
	-Db_ndebug=true \
	-Dc_args="-march=${CPU_NAME}" \
	-Dcpp_args="-march=${CPU_NAME}" \
	-Dlibdir="lib"

	meson compile -C build
}

package_libwlroots10() {
	pkgdesc="Modular wayland compositor library -- runtime"
	depends=("libc6" "libdrm2" "libinput10" "libudev1" "libgbm1" "libxkbcommon0" "libpixman-1-0"
			 "libvulkan1" "libwayland-server0" "libwayland-client0" "libseat1")

	cd ${pkgbase}-${pkgver}

	mkdir "${pkgdir}/tmp"
	mkdir -p "${pkgdir}/${prefix}/lib"

	meson install -C build --destdir "${pkgdir}/tmp"
	cp -P ${pkgdir}/tmp/${prefix}/lib/libwlroots.so* "${pkgdir}/${prefix}/lib"

	rm -r "${pkgdir}/tmp"
}

package_libwlroots-dev() {
	pkgdesc="Modular wayland compositor library -- development files"
	depends=("libwlroots10")

	cd ${pkgbase}-${pkgver}

	mkdir "${pkgdir}/tmp"
	mkdir -p "${pkgdir}/${prefix}/lib"

	meson install -C build --destdir "${pkgdir}/tmp"
	cp -r "${pkgdir}/tmp/${prefix}/include" "${pkgdir}/${prefix}"
	cp -r "${pkgdir}/tmp/${prefix}/lib/pkgconfig" "${pkgdir}/${prefix}/lib"

	rm -r "${pkgdir}/tmp"
}
